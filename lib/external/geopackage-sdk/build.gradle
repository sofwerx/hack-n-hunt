apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "mil.nga.geopackage"
archivesBaseName = "geopackage-android"
version = "2.0.1"
sourceCompatibility = 1.6
def remotePublish = false

android {
    compileSdkVersion Integer.valueOf(project.ext.androidCompileSDKVersion)
    buildToolsVersion project.ext.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion Integer.valueOf(project.ext.androidMinSDKVersion)
        targetSdkVersion Integer.valueOf(project.ext.androidTargetSDKVersion)

        testApplicationId 'mil.nga.geopackage.test'
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
        }

        lintOptions {
            abortOnError false
        }
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.java.srcDirs
    }

    artifacts {
        archives sourcesJar
    }

    signing {
        required {remotePublish}
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {

                if (!remotePublish) {

                    repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository')

                } else {

                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name 'GeoPackage Android'
                        packaging 'aar'
                        description 'GeoPackage Android implementation'
                        url 'https://github.com/ngageoint/geopackage-android'

                        scm {
                            url 'git@github.com:ngageoint/geopackage-android.git'
                            connection 'scm:git:git@github.com:ngageoint/geopackage-android.git'
                            developerConnection 'scm:git:git@github.com:ngageoint/geopackage-android.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'https://github.com/ngageoint/geopackage-android/blob/master/LICENSE.txt'
                                distribution 'repo'
                            }
                        }

                        organization {
                            name 'National Geospatial-Intelligence Agency'
                            url 'https://www.nga.mil'
                        }

                        developers {
                            developer {
                                id 'bosborn'
                                name 'Brian Osborn'
                                email 'osbornb@bit-sys.com'
                                organizationUrl 'https://www.bit-sys.com'
                                roles {
                                    role 'developer'
                                }
                                timezone 'UTC-07'
                            }
                        }
                    }
                }
            }
        }
    }

}

dependencies {
    compile 'com.android.support:support-v13:27.1.1'
    compile 'com.j256.ormlite:ormlite-android:5.0'
    compile 'mil.nga.geopackage:geopackage-core:2.0.0'
    compile 'ar.com.hjg:pngj:2.1.0'
    compile 'mil.nga:tiff:2.0.0'
}
